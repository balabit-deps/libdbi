dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/dbi_main.c)

AM_INIT_AUTOMAKE(libdbi, 0.6.5)
AM_CONFIG_HEADER(config.h)

dnl Library versioning
LIB_CURRENT=0
LIB_REVISION=4
LIB_AGE=0
AC_SUBST(LIB_CURRENT)
AC_SUBST(LIB_REVISION)
AC_SUBST(LIB_AGE)

AC_CANONICAL_HOST

driverdir=$libdir/dbd
AC_SUBST(driverdir)

dnl ====================================
dnl Check for programs
dnl ====================================

AC_PROG_CC
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

dnl ====================================
dnl Set some general compile options
dnl ====================================

if test -z "$GCC"; then
        case $host in
        *-*-irix*)
                if test -z "$CC"; then
                        CC=cc
                fi
                DEBUG="-g -signed"
                CFLAGS="-O2 -w -signed"
                PROFILE="-p -g3 -O2 -signed" ;;
        sparc-sun-solaris*)
                DEBUG="-v -g"
                CFLAGS="-xO4 -fast -w -fsimple -native -xcg92"
                PROFILE="-v -xpg -g -xO4 -fast -native -fsimple -xcg92 -Dsuncc" ;;
        *)
                DEBUG="-g"
                CFLAGS="-O"
                PROFILE="-g -p" ;;
        esac
else

        case $host in
        *-*-linux*)
                DEBUG="-g -Wall -D_REENTRANT -D__NO_MATH_INLINES -fsigned-char"
                CFLAGS="-O20 -ffast-math -D_REENTRANT -fsigned-char"
                PROFILE="-pg -g -O20 -ffast-math -D_REENTRANT -fsigned-char";;
        sparc-sun-*)
                DEBUG="-g -Wall -D__NO_MATH_INLINES -fsigned-char -mv8"
                CFLAGS="-O20 -ffast-math -D__NO_MATH_INLINES -fsigned-char -mv8"
                PROFILE="-pg -g -O20 -D__NO_MATH_INLINES -fsigned-char -mv8" ;;
        *)
                DEBUG="-g -Wall -D__NO_MATH_INLINES -fsigned-char"
                CFLAGS="-O20 -D__NO_MATH_INLINES -fsigned-char"
                PROFILE="-O20 -g -pg -D__NO_MATH_INLINES -fsigned-char" ;;
        esac
fi


AC_SUBST(DEBUG)
AC_SUBST(PROFILE)

case $host in
	*-*-*bsd*)
		dlopen_flag="RTLD_LAZY" ;;
	*)
		dlopen_flag="RTLD_NOW" ;;
esac

AC_DEFINE_UNQUOTED(DLOPEN_FLAG, $dlopen_flag, [ ])

dnl ==============================
dnl Check for libraries
dnl ==============================

AC_CHECK_MYSQL
AC_CHECK_PGSQL

dnl ==============================
dnl Check for functions
dnl ==============================

case $host in
	*-*-win32*)
		shlib_ext=.dll ;;
	*)
		shlib_ext=.so ;;
esac

AC_CHECK_FUNCS(strtoll)
AC_REPLACE_FUNCS(atoll)
AC_CHECK_FUNCS(vasprintf)
AC_REPLACE_FUNCS(asprintf)
AC_DEFINE_UNQUOTED(DRIVER_EXT, "$shlib_ext", [ ])

dnl ==============================
dnl Checks for header files
dnl ==============================

AC_CHECK_HEADERS(string.h strings.h)

AC_OUTPUT([
	libdbi.spec
	Makefile
	src/Makefile
	doc/Makefile
	include/Makefile
	include/dbi/Makefile
	drivers/Makefile
	drivers/mysql/Makefile
	drivers/postgresql/Makefile
	tests/Makefile
	])
